apply plugin: 'android'
apply plugin: 'android-test'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    productFlavors {
        free {
            packageName "com.battlelancer.seriesguide"
            versionCode 199
            versionName "11-beta1"
        }
        beta {
            packageName "com.battlelancer.seriesguide.beta"
            versionCode 215
            versionName "11-internal"
        }
    }

    signingConfigs {
        release {
            storeFile file('release.keystore')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile project(':libraries:androidutils')
    compile project(':libraries:pagerslidingtabstrip')
    compile project(':libraries:stickygridheaders')
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:19.0.+'
    compile 'com.github.chrisbanes.photoview:library:1.2.2'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.+'
    compile 'com.squareup.okhttp:okhttp:1.2.1'
    compile 'com.squareup.retrofit:retrofit:1.2.2'
    compile 'de.greenrobot:eventbus:2.2.0'

    // OAuth 2.0
    compile 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:0.31'

    // App Engine
    compile ('com.google.api-client:google-api-client-android:1.17.0-rc') {
        exclude group: 'com.google.android.google-play-services'
    }
    compile 'com.google.http-client:google-http-client-gson:1.17.0-rc'
    compile 'com.google.android.gms:play-services:4.0.30'

    // libs folder
    compile fileTree(dir: 'libs', include: '*.jar')

    testCompile 'junit:junit:4.10'
    testCompile 'com.squareup:fest-android:1.0.+'
    testCompile 'org.robolectric:robolectric:2.2'
}

// get signing config from gradle.properties file
if (project.hasProperty('storePassword')
        && project.hasProperty('keyAlias')
        && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
