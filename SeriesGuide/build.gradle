apply plugin: 'android'

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.1"
    compileOptions.encoding = "UTF-8"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 18
    }

    productFlavors {
        free {
            packageName "com.battlelancer.seriesguide"
            versionCode 179
            versionName "8.2"
        }
        beta {
            packageName "com.battlelancer.seriesguide.beta"
            versionCode 215
            versionName "8-internal"
        }
    }

    signingConfigs {
        release {
            storeFile file('release.keystore')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile project(':libraries:android-menudrawer')
    compile project(':libraries:androidutils')
    compile project(':libraries:pagerslidingtabstrip')
    compile project(':libraries:stickygridheaders')
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.android.support:support-v4:18.0.+'
}

// get signing config from gradle.properties file
if (project.hasProperty('storePassword')
        && project.hasProperty('keyAlias')
        && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}