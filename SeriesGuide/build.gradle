apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def loadStringPropertyOrPlaceholder(String name) {
    return rootProject.hasProperty(name) ? rootProject.property(name) : "MISSING"
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        // only enable if debug builds exceed 64K method limit
        multiDexEnabled false

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "TVDB_API_KEY", "\"${loadStringPropertyOrPlaceholder("TVDB_API_KEY")}\"")
        buildConfigField("String", "TMDB_API_KEY", "\"${loadStringPropertyOrPlaceholder("TMDB_API_KEY")}\"")
        buildConfigField("String", "TRAKT_CLIENT_ID", "\"${loadStringPropertyOrPlaceholder("TRAKT_CLIENT_ID")}\"")
        buildConfigField("String", "TRAKT_CLIENT_SECRET", "\"${loadStringPropertyOrPlaceholder("TRAKT_CLIENT_SECRET")}\"")
        buildConfigField("String", "IAP_KEY_A", "\"${loadStringPropertyOrPlaceholder("IAP_KEY_A")}\"")
        buildConfigField("String", "IAP_KEY_B", "\"${loadStringPropertyOrPlaceholder("IAP_KEY_B")}\"")
        buildConfigField("String", "IAP_KEY_C", "\"${loadStringPropertyOrPlaceholder("IAP_KEY_C")}\"")
        buildConfigField("String", "IAP_KEY_D", "\"${loadStringPropertyOrPlaceholder("IAP_KEY_D")}\"")
        buildConfigField("String", "IMAGE_CACHE_URL", "\"${loadStringPropertyOrPlaceholder("IMAGE_CACHE_URL")}\"")
        buildConfigField("String", "IMAGE_CACHE_SECRET", "\"${loadStringPropertyOrPlaceholder("IMAGE_CACHE_SECRET")}\"")

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ eventBusIndex : 'com.battlelancer.seriesguide.SgEventBusIndex' ]
            }
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    productFlavors {
        free {
            applicationId "com.battlelancer.seriesguide"
            versionCode versions.code
            versionName versions.name

            manifestPlaceholders = [
                    androidBackupKey: rootProject.hasProperty("FREE.ANDROID_BACKUP_KEY")
                            ? rootProject.property("FREE.ANDROID_BACKUP_KEY") : "MISSING"
            ]
        }
        amazon {
            applicationId "com.uwetrottmann.seriesguide.amzn"
            versionCode versions.code
            versionName versions.name
        }
        // temporarily unused
//        preview {
//            applicationId "com.uwetrottmann.seriesguide.preview"
//            versionCode versions.code
//            versionName versions.name + "-preview"
//        }
    }

    signingConfigs {
        release {
            if(rootProject.file('keystore.properties').exists()) {
                def props = new Properties()
                props.load(new FileInputStream(rootProject.file('keystore.properties')))

                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if(rootProject.file('keystore.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            // disable shrinking to use incremental dex in builds
            minifyEnabled false
            // en_XA (LTR) and ar_XB (RTL) to test UI adjusting to unusual glyphs and long strings
            // keep disabled unless needed, slows down build
            pseudoLocalesEnabled false
            // disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
    }
}

endpointsClient {
    discoveryDocs = ['src/endpoints/account-v2-rest.discovery',
                     'src/endpoints/episodes-v2-rest.discovery',
                     'src/endpoints/lists-v2-rest.discovery',
                     'src/endpoints/movies-v2-rest.discovery',
                     'src/endpoints/shows-v2-rest.discovery']
}

dependencies {
    compile project(':api')
    compile project(':libraries:drag-sort-listview')
    compile project(':libraries:stickygridheaders')

    compile "com.android.support:support-v4:${versions.supportLibrary}"
    compile "com.android.support:support-annotations:${versions.supportLibrary}"
    compile "com.android.support:appcompat-v7:${versions.supportLibrary}"
    compile "com.android.support:cardview-v7:${versions.supportLibrary}"
    compile "com.android.support:customtabs:${versions.supportLibrary}"
    compile "com.android.support:design:${versions.supportLibrary}"
    compile "com.android.support:palette-v7:${versions.supportLibrary}"
    compile "com.android.support:recyclerview-v7:${versions.supportLibrary}"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile "com.jakewharton:butterknife:${versions.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.butterknife}"
    compile "com.google.dagger:dagger:${versions.dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${versions.dagger}"
    compile 'org.greenrobot:eventbus:3.0.0'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'

    compile("com.crashlytics.sdk.android:crashlytics:${versions.crashlytics}@aar") {
        transitive = true
    }
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile "com.google.code.gson:gson:${versions.gson}"
    compile "com.jakewharton.threetenabp:threetenabp:${versions.threetenABP}"
    compile "com.jakewharton.timber:timber:${versions.timber}"
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'

    compile "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    compile ("com.uwetrottmann.androidutils:androidutils:${versions.androidUtils}") {
        exclude group: 'com.android.support'
    }
    compile 'com.uwetrottmann.photoview:library:1.2.4'
    compile "com.uwetrottmann.thetvdb-java:thetvdb-java:${versions.thetvdb}"
    compile "com.uwetrottmann.tmdb2:tmdb-java:${versions.tmdb}"
    compile ("com.uwetrottmann.trakt5:trakt-java:${versions.trakt}") {
        exclude group: 'org.threeten', module: 'threetenbp' // using ThreeTenABP instead
    }

    // https://github.com/palaima/DebugDrawer
    debugCompile 'io.palaima.debugdrawer:debugdrawer-view:0.7.0'
    releaseCompile 'io.palaima.debugdrawer:debugdrawer-view-no-op:0.7.0'
    compile 'io.palaima.debugdrawer:debugdrawer-commons:0.7.0'
    compile 'io.palaima.debugdrawer:debugdrawer-actions:0.7.0'
    compile 'io.palaima.debugdrawer:debugdrawer-timber:0.7.0'

    // Google Play Services
    compile "com.google.android.gms:play-services-analytics:${versions.playServices}"
    compile "com.google.android.gms:play-services-auth:${versions.playServices}"

    // App Engine
    // https://github.com/google/google-api-java-client/commits/dev
    compile('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'com.google.android.google-play-services' // including latest ourselves
        exclude group: 'org.apache.httpcomponents', module: 'httpclient' // unused
        exclude group: 'com.google.guava', module: 'guava-jdk5' // out of date, conflict with dagger
    }

    // Amazon flavor specific
    amazonCompile files('libs/amazon/in-app-purchasing-2.0.76.jar')

    // Instrumented unit tests
    androidTestCompile "com.android.support:support-annotations:${versions.supportLibrary}"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "com.squareup.assertj:assertj-android:${versions.assertjAndroid}"

    // Local unit tests
    testCompile 'junit:junit:4.12'
    testCompile "com.android.support:support-annotations:${versions.supportLibrary}"
    testCompile "com.squareup.assertj:assertj-android:${versions.assertjAndroid}"
    // include regular threetenbp to have access to the tz database without init call
    testCompile 'org.threeten:threetenbp:1.3.3'

}
