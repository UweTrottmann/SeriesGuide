// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.2.30'
    ext.versions = [
            'minSdk': 15,
            'compileSdk': 27,
            'targetSdk': 26,
            'buildTools': '27.0.1',

            'supportLibrary': '27.0.2', // developer.android.com/topic/libraries/support-library/revisions.html
            'lifecycle': '1.1.0', // developer.android.com/topic/libraries/architecture/release-notes.html
            'playServices': '11.4.0', // developers.google.com/android/guides/releases

            'butterknife': '8.8.1', // github.com/JakeWharton/butterknife/blob/master/CHANGELOG.md
            'crashlytics': '2.9.1', // docs.fabric.io/android/changelog.html
            'dagger': '2.13', // github.com/google/dagger/releases
            'gson': '2.8.2', // github.com/google/gson/blob/master/CHANGELOG.md
            'okhttp': '3.10.0', // github.com/square/okhttp/blob/master/CHANGELOG.md
            'timber': '4.6.1', // github.com/JakeWharton/timber/blob/master/CHANGELOG.md

            'androidUtils': '2.3.1', // github.com/UweTrottmann/AndroidUtils/blob/master/RELEASE_NOTES.md
            'thetvdb': '1.5.0', // github.com/UweTrottmann/thetvdb-java/blob/master/CHANGELOG.md
            'tmdb': '1.8.3', // github.com/UweTrottmann/tmdb-java/blob/master/CHANGELOG.md
            'trakt': '5.7.3', // github.com/UweTrottmann/trakt-java/blob/master/CHANGELOG.md

            'truth': '0.39',
            'threetenABP': '1.0.6', // github.com/UweTrottmann/ThreeTenABP/blob/master/CHANGELOG.md

            // version 15xxxyy -> min SDK 15, release xxx, build yy
            'code': 1503005,
            'name': '42',
    ]

    repositories {
        jcenter()
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1' // libraries, SeriesGuide
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.3' // SeriesGuide
        classpath 'io.fabric.tools:gradle:1.25.2' // SeriesGuide
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.10.0' // api
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        jcenter()
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
}

task clean(type: Delete) {
    group "build"
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    group "build setup"
    gradleVersion = '4.5'
    distributionType = Wrapper.DistributionType.ALL
}
